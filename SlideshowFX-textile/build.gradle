description = 'Extension allowing to define slide\'s content using the textile syntax'
version = '1.1'

configurations {
    wikitext
}

dependencies {
    wikitext "org.eclipse.mylyn.docs:wikitext.core:$wikitextVersion", "org.eclipse.mylyn.docs:org.eclipse.mylyn.wikitext.textile:$wikitextVersion"

    compile project(':SlideshowFX-markup')

    compile "org.apache.felix:org.apache.felix.framework:$felixVersion"
    compile configurations.wikitext

    testCompile "junit:junit:$junitVersion"
}

jar {
    from(configurations.wikitext.resolve().collect()) {
        include '*'
    }

    manifest {
        attributes('Manifest-Version': '1.0',
                'Bundle-ManifestVersion': '2',
                'Bundle-Name': 'SlideshowFX Textile support',
                'Bundle-SymbolicName': 'com.twasyl.slideshowfx.markup.textile',
                'Bundle-Description': 'Support Textile for defining slide\'s content',
                'Bundle-Version': "$project.version",
                'Bundle-Activator': 'com.twasyl.slideshowfx.markup.textile.activator.TextileActivator',
                'Bundle-ClassPath': configurations.wikitext.resolve().collect { artifact -> artifact.name }.join(',').concat(',.'),
                'Bundle-Vendor': 'Thierry Wasylczenko',
                'Export-Package': 'com.twasyl.slideshowfx.markup.textile,com.twasyl.slideshowfx.markup.textile.activator',
                'Import-Package': 'org.osgi.framework',
                'Setup-Wizard-Label': 'Textile')
    }
}

bintray {

    configurations = ['archives']

    pkg {
        version {
            name = project.version
            desc = project.description
            released = new Date()
            vcsTag = "v${project.version}"
        }
    }
}

tasks.bintrayUpload.enabled = textileMarkupBintrayUploadEnabled