description = 'Extension allowing to define slide\'s content using the mardown syntax'
version = '1.0'

configurations {
    markdown
}

dependencies {
    markdown "com.github.rjeschke:txtmark:$markdownVersion"

    compile project(':SlideshowFX-markup')

    compile "org.apache.felix:org.apache.felix.framework:$felixVersion"
    compile configurations.markdown

    testCompile "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

jar {
    from(configurations.markdown.resolve().collect()) {
        include '*'
    }

    manifest {
        attributes('Manifest-Version': '1.0',
                'Bundle-ManifestVersion': '2',
                'Bundle-Name': 'SlideshowFX markdown support',
                'Bundle-SymbolicName': 'com.twasyl.slideshowfx.markup.markdown',
                'Bundle-Description': 'Support markdown for defining slide\'s content',
                'Bundle-Version': "$project.version",
                'Bundle-Activator': 'com.twasyl.slideshowfx.markup.markdown.activator.MarkdownActivator',
                'Bundle-ClassPath': configurations.markdown.resolve().collect { artifact -> artifact.name }.join(',').concat(',.'),
                'Bundle-Vendor': 'Thierry Wasylczenko',
                'Export-Package': 'com.twasyl.slideshowfx.markup.markdown,com.twasyl.slideshowfx.markup.markdown.activator',
                'Import-Package': 'org.osgi.framework',
                'Setup-Wizard-Label': 'Markdown')
    }
}

bintray {

    configurations = ['archives']

    pkg {
        version {
            name = project.version
            desc = project.description
            released = new Date()
            vcsTag = "v${project.version}"
        }
    }
}

tasks.bintrayUpload.enabled = markdownMarkupBintrayUploadEnabled