description = 'Hosting connector allowing to open and save presentations from and to Dropbox'
version = '1.1'

dependencies {
    compile project(':SlideshowFX-global-configuration')
    compile project(':SlideshowFX-hosting-connector')

    compile configurations.felix
    compile configurations.dropbox
}

jar {
    from(configurations.dropbox.resolve().collect()) {
        include '*'
    }

    manifest {
        attributes('Manifest-Version': '1.0',
                'Bundle-ManifestVersion': '2',
                'Bundle-Name': 'SlideshowFX Dropbox hosting connector',
                'Bundle-SymbolicName': 'com.twasyl.slideshowfx.hosting.connector.dropbox',
                'Bundle-Description': 'Support for connecting to Dropbox',
                'Bundle-Version': version,
                'Bundle-Activator': 'com.twasyl.slideshowfx.hosting.connector.dropbox.activator.DropboxHostingConnectorActivator',
                'Bundle-ClassPath': configurations.dropbox.resolve().collect { artifact -> artifact.name }.join(',').concat(',.'),
                'Bundle-Vendor': 'Thierry Wasylczenko',
                'Export-Package': 'com.twasyl.slideshowfx.hosting.connector.dropbox,com.twasyl.slideshowfx.hosting.connector.dropbox.activator',
                'Import-Package': 'org.osgi.framework',
                'Setup-Wizard-Label': 'Dropbox')
    }
}

bintray {

    configurations = ['archives']

    pkg {
        version {
            name = project.version
            desc = project.description
            released = new Date()
            vcsTag = "v${project.version}"
        }
    }
}

tasks.bintrayUpload.enabled = dropboxHostingConnectorBintrayUploadEnabled